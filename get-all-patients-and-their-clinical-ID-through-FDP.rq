#+ endpoint: http://lumc-beat-covid.fair-dtls.surf-hosted.nl:8090/blazegraph/namespace/kb/sparql
#+ endpoint_in_url: False
#+ description: Get all patients and their clinical ID from a Covid-19 synthetic dataset found via a FDP.
#+ defaults:
#+   - type: http://purl.obolibrary.org/obo/NCIT_C25261
#+   - use: http://purl.obolibrary.org/obo/MONDO_0100096
#+   - relation: http://semanticscience.org/resource/SIO_001410

# LUMC prefixes
prefix dcat:     <http://www.w3.org/ns/dcat#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX dct:  <http://purl.org/dc/terms/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX ncit:    <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX sio:     <http://semanticscience.org/resource/>
PREFIX prov: <http://http://http://www.w3.org/ns/prov#>

# Get all patients and their clinical ID

SELECT DISTINCT ?person ?id WHERE {

?beatcovid_dataset a dcat:Dataset ;
    dcat:type ?_type_iri ;
    obo:DUO_0000010 ?_use_iri ;
    obo:RO_0000086 ?_relation_iri ;
    dcat:distribution ?beatcovid_dataset_dist .

?beatcovid_dataset_dist dcat:accessURL ?accessURL .

SERVICE ?accessURL {
      # Get all person
      ?person a sio:SIO_000776;
              sio:SIO_000228 ?patient_role.

      # Get all person who plays patient role
      ?patient_role a obo:OBI_0000093.

      # Get patient id (clinical ID)
      OPTIONAL {
          ?id_node a obo:NCIT_C164337;
                   sio:SIO_000300 ?id;
                   obo:IAO_0000219 ?patient_role.
      }
    }
}
